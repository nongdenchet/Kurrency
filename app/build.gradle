apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'jacoco'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    testBuildType "mock"

    defaultConfig {
        applicationId "com.rain.currency"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 8
        versionName "1.1.1"

        buildConfigField "String", "BASE_URL", '"https://kurrency.herokuapp.com/api/"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled = true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            buildConfigField "String", "ACCESS_TOKEN", AccessToken
        }

        mock {
            initWith(debug)
            buildConfigField "String", "BASE_URL", '"http://localhost:8000/"'
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            buildConfigField "String", "ACCESS_TOKEN", AccessToken
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "Kurrency-${variant.versionName}.apk"
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Custom View
    implementation 'de.hdodenhof:circleimageview:2.2.0'

    // Firebase
    implementation 'com.google.firebase:firebase-crashlytics:17.0.1'
    implementation 'com.google.firebase:firebase-analytics:17.4.3'

    // Image Handling
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    // Dagger
    implementation 'com.google.dagger:dagger:2.27'
    implementation 'com.google.dagger:dagger-android:2.25.2'
    implementation 'com.google.dagger:dagger-android-support:2.25.2'
    kapt 'com.google.dagger:dagger-compiler:2.25.2'
    kapt 'com.google.dagger:dagger-android-processor:2.25.2'

    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    // Butter Knife
    implementation 'com.jakewharton:butterknife:10.2.0'
    kapt 'com.jakewharton:butterknife-compiler:10.2.0'

    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.6.4'
    implementation 'com.squareup.retrofit2:converter-moshi:2.5.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.6.4'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.14.9'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.10'

    // Parsing
    implementation 'com.squareup.moshi:moshi:1.9.3'
    kapt 'com.squareup.moshi:moshi-kotlin-codegen:1.9.3'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.2.0'

    // Android test
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.12.10'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.2.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}
